#!/usr/bin/env osascript
on run argv 
  set tabName to item 1 of argv
tell application "Google Chrome"
  activate
  repeat with w in (windows)
          set j to 0
          repeat with t in (tabs of w)
              set j to j + 1
              if title of t contains tabName then
                  set (active tab index of w) to j
                  set index of w to 1
                  tell application "System Events" to tell process "Google Chrome"
                      perform action "AXRaise" of window 1 -- `set index` doesn't always raise the window
                  end tell
                  return
              end if
          end repeat
      end repeat
  end tell
end